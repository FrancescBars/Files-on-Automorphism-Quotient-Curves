Here we provide Magma code with explanation for computing the values of (q,M,W) that satisfies the inequalities (5.46),(5.47) and (5.48) in Theorem 34 (i.e., for the case $n=2$ and $q=2$) in the paper ``On automorphisms of quotient modular curves". The explanations of the Magma codes used for checking the other inequalities in Theorem 34 are very similar to this.

The main idea is: We first rewrite the program to compute the genus of the quotient modular curve $X_0(N)/W$ (recall that $N$ is square-free). The command ``genereX0NQuotientWN(N,TN,t)`` computes the genus of the curve $X_0(N)/W$, where ``TN:=[d | w_d belongs to W]`` and ``t:=#TN``. For example, if $W$ is generated by the Atkin-Lehner involutions $w_{d1}, w_{d2}$ and $w_{d3}$, then ``TN :=[d1,d2,d3,Squarefreepart(d1*d2), Squarefreepart(d1*d3), Squarefreepart(d2*d3),Squarefreepart(d1*d2*d3),1]``, where ``Squarefreepart(d)`` denotes the squarefree part of a natural number ``d``. 
Then we rewrite a program to compute the points of the quotient curve $X_0(N)/W$ over a finite field $\mathbb{F}_{q^k}$ (q is a prime not dividing N). 
Then we write a program to check the sign of $|X_0(N)/W (\mathbb{F}_{q^k}|-degree\cdot |\mathbb{P}^1(\mathbb{F}_{q^k})|$, where ``degree`` denotes a natural number. 
The command ``MapdegreedtoP1(q,degree,bound,FpnpointsQuotientCurve)`` list if any of the set $(q^k, z)$ with $k<\mathrm{bound}$ such that $|X_0(N)/W \mathbb{F}_{q^k})|-degree\cdot |\mathbb{P}^1(\mathbb{F}_{q^k})|=z$, where z is a positive integer. Thus no $degree$ map between the quotient modular curve $X_0(N)/W$ to the projective space $\mathbb{P}^1$ defined over $\mathbb{F}_{q^k}$, if the list is non empty (recall q does not divide N).  
Then we check the inequalities (5.46), (5.47) and (5.48)  case by case i.e., first for order(W)=2, then for order(W)=4, then order(W)=8 and then order(W)=16.





##Example 

First we pick different functions for computing the genus of a quotient modular curve (such Magma codes are already available in another folder in this github).

````magma

fixedpointsALinvsmall:=function(m, n)

if n eq 3 then
q:=Factorization(Numerator(m/3));
nu_3:=2;
for d in [1 .. #q] do
if q[d][1] eq 2 then
if q[d][2] gt 2 then
nu_3:=0;
else
end if;
else
nu_3:=nu_3*(1+LegendreSymbol(-n,q[d][1]));
end if;
end for;
return nu_3;
else
if n eq 1 then
else
if n eq 2 then
q:=PrimeDivisors(Numerator(m/n));
nu_2factorminus1:=1;
nu_2factorminus2:=1;
for d in [1 .. #q] do
nu_2factorminus1:=nu_2factorminus1*(1+LegendreSymbol(-1,q[d]));
nu_2factorminus2:=nu_2factorminus2*(1+LegendreSymbol(-2,q[d]));
end for;
return nu_2factorminus1+nu_2factorminus2;
else
if n eq 4 then
q:=PrimeDivisors(Numerator(m/n));
l:=Divisors(Numerator(m/n));
nu_4factorminus1:=1; nu_4sumeuler:=0;
for d in [1..#q] do
nu_4factorminus1:=nu_4factorminus1*(1+LegendreSymbol(-1,q[d]));
end for;
for dd in [1..#l] do
ss:=Numerator(m/(n*l[dd]));
ssh:=GCD(l[dd],ss);
nu_4sumeuler:=nu_4sumeuler+EulerPhi(ssh);
end for;
return nu_4factorminus1+nu_4sumeuler;
end if;
end if;
end if;
end if;

end function;



fixedpointsALinvbig:=function(m, n)

PD:=PrimeDivisors(Numerator(m/n)); D:=Divisors(Numerator(m/n));

n_mod2:=n mod 2; n_mod4:=n mod 4;


if n_mod2 eq 1 then
if n_mod4 eq 3 then
if 2 in PD then
if 8 in D then
nu_nodd3mod4_8:=2*(ClassNumber(-n)+ClassNumber(-4*n))*(1+KroneckerSymbol(-n,2));
if #PD eq 1 then
else
for p1 in PD do
p1_mod2:=p1 mod 2;
if p1_mod2 eq 1 then
nu_nodd3mod4_8:=nu_nodd3mod4_8*(1+LegendreSymbol(-n,p1));
end if;
end for;
end if;
return nu_nodd3mod4_8;
else
if 4 in D then
nu_nodd3mod4_4:=
(2*ClassNumber(-4*n)+2*(1+KroneckerSymbol(-n,2))*ClassNumber(-n));
if #PD eq 1 then
return nu_nodd3mod4_4;
else
for p2 in PD do
p2_mod2:=p2 mod 2;
if p2_mod2 eq 1 then
nu_nodd3mod4_4:=nu_nodd3mod4_4*(1+LegendreSymbol(-n,p2));
end if;
end for;
return nu_nodd3mod4_4;
end if;
else
nu_nodd3mod4_2:=ClassNumber(-4*n)+3*ClassNumber(-n);
if #PD eq 1 then
return nu_nodd3mod4_2;
else
for p3 in PD do
p3_mod2:=p3 mod 2;
if p3_mod2 eq 1 then
nu_nodd3mod4_2:=nu_nodd3mod4_2*(1+LegendreSymbol(-n,p3));
end if;
end for;
return nu_nodd3mod4_2;
end if;
end if;
end if;
else
if #PD eq 0 then
nu_nodd3mod4_no2:=ClassNumber(-n)+ClassNumber(-4*n);
return nu_nodd3mod4_no2;
else
nu_nodd3mod4_no2:=ClassNumber(-n)+ClassNumber(-4*n);
for j in [1..#PD] do
nu_nodd3mod4_no2:=nu_nodd3mod4_no2*(1+LegendreSymbol(-n,PD[j]));
end for;
return nu_nodd3mod4_no2;
end if;
end if;
else
if #PD eq 0 then
nu_nodd1mod4:=ClassNumber(-4*n);
return nu_nodd1mod4;
else
if 2 in PD then
if 4 in D then
return 0;
else
nu_nodd1mod4_2:=ClassNumber(-4*n);
PD2:=PrimeDivisors(Numerator(m/(2*n)));
for k in [1..#PD2] do
nu_nodd1mod4_2:=nu_nodd1mod4_2*(1+LegendreSymbol(-n,PD2[k]));
end for;
return nu_nodd1mod4_2;
end if;
else
nu_nodd1mod4_no2:=ClassNumber(-4*n);
for i in [1 .. #PD] do
nu_nodd1mod4_no2:=nu_nodd1mod4_no2*(1+LegendreSymbol(-n,PD[i]));
end for;
return nu_nodd1mod4_no2;
end if;
end if;
end if;
else
nu_neven:=ClassNumber(-4*n);
if #PD eq 0 then
return nu_neven;
else
for u in [1 .. #PD] do
nu_neven:=nu_neven*(1+LegendreSymbol(-n,PD[u]));
end for;
return nu_neven;
end if;
end if;
end function;


generexoN:=function(b)

m:=PrimeDivisors(b);
l:=Divisors(b);
factor_b:=Factorization(b);
psiEulerindex:=b;
order4elliptic:=1;
order3elliptic:=1;
cusps:=0;

for x in [1 .. #m] do
psiEulerindex:=psiEulerindex*(1+1/m[x]);
end for;

for y in [1..#m] do
if factor_b[y][1] eq 2 then
order3elliptic:=0;
if factor_b[y][2] gt 1 then
order4elliptic:=0;
end if;
else
if factor_b[y][1] eq 3 then
if factor_b[y][2] gt 1 then
order3elliptic:=0;
order4elliptic:=order4elliptic*(1+LegendreSymbol(-1,factor_b[y][1]));
else
order3elliptic:=order3elliptic*(1+LegendreSymbol(-3,factor_b[y][1]));
order4elliptic:=order4elliptic*(1+LegendreSymbol(-1,factor_b[y][1]));
end if;
else
order4elliptic:=order4elliptic*(1+LegendreSymbol(-1,factor_b[y][1]));
order3elliptic:=order3elliptic*(1+LegendreSymbol(-3,factor_b[y][1]));
end if;
end if;
end for;

for a in [1 .. #l] do
n1:=Numerator(b/l[a]);
t1:=GCD(l[a],n1);
cusps:=cusps+EulerPhi(t1);
end for;

genus:=1+(psiEulerindex/12)-(order4elliptic/4)-(order3elliptic/3)-(cusps/2);
return genus;
end function;


genereX0NQuotientWN:=function(N,WN,t);

FixedpointsALinvolutions:=[* *]; vv:=0; L:=Divisors(N);


for i in [1..#WN] do
u:=GCD(WN[i],Numerator(N/WN[i]));
if WN[i] in L then
else
vv:=1;
end if;

if u eq 1 then
if WN[i] eq 1 then
else
if WN[i] gt 4 then
nu_Ddi:=fixedpointsALinvbig(N,WN[i]);
FixedpointsALinvolutions:=Append(FixedpointsALinvolutions,nu_Ddi);
else
nu_Ddi:=fixedpointsALinvsmall(N,WN[i]);
FixedpointsALinvolutions:=Append(FixedpointsALinvolutions,nu_Ddi);
end if;
end if;
else
vv:=1;
end if;

end for;

CountAllFixedPointsALinvolutions:=0;

for u in FixedpointsALinvolutions do
CountAllFixedPointsALinvolutions:=CountAllFixedPointsALinvolutions+u;
end for;
if vv eq 0 then
genusxoN:=generexoN(N);
genusxoNQuotient:=1+t^(-1)*(genusxoN-1-(CountAllFixedPointsALinvolutions/2));
else
genusxoNQuotient:=-1;
end if;
return genusxoNQuotient;

end function;
````
The command ``genereX0NQuotientWN(N,TN,t)`` computes the genus of the curve $X_0(N)/W$, where ``TN:=[d | w_d belongs to W]`` and ``t:=#TN``. For example, if $W$ is generated by the Atkin-Lehner involutions $w_{d1}, w_{d2}$ and $w_{d3}$, then ``TN :=[d1,d2,d3,Squarefreepart(d1*d2), Squarefreepart(d1*d3), Squarefreepart(d2*d3),Squarefreepart(d1*d2*d3),1]``, where ``Squarefreepart(d)`` denotes the squarefree part of a natural number ``d``.

````magma

JacobianDecompositionQuotientX0NWN:=function(n,WN,length,t); N:=n;
AtkinLehnerfix:=WN;
Involutions:=#AtkinLehnerfix;
L:=[**];
F:=[**];
Level:=[* *];
ALaction:=[**];

Pd:=PrimeDivisors(N); mt:=[**];

for prime in Pd do
op:=Valuation(N,prime);
primepower:=Gcd(prime^(op), N);

mt:=Append(mt, primepower);
end for;


Nd:=Divisors(N); countergenus:=0;

for j in Nd do

MS:=NewformDecomposition(CuspidalSubspace(ModularSymbols(j,2,1)));

m:=#MS;

M:=PrimeDivisors(j);

Nr:=Numerator(N/j);

divi:=GCD(j,Nr);

jj:=Numerator(j/divi);

Mm:=PrimeDivisors(jj);

Nn:=Divisors(jj);

mm:=#Mm;

mn:=#Nn;

D:=Factorization(jj);

for i in [1..m] do

f:=Eigenform(MS[i],length);

f2:=MS[i];

K:=Parent(Coefficient(f,3)); d:=Dimension(MS[i]);

X:=IdentityMatrix(Rationals(), d);

u:=0;
for jo in [1..Involutions] do
dd:=GCD(j,AtkinLehnerfix[jo]);

if dd eq AtkinLehnerfix[jo] then

Y:=AtkinLehner(MS[i],dd);

if Y eq X then

else

u:=1;

end if;

else

if dd eq 1 then

else

end if;

end if;

end for;

if u eq 0 then
ALactionf:=[**];
for i in [1..#Pd] do
if GCD(mt[i],j) eq mt[i] then

ALactionf:=Append(ALactionf, [*AtkinLehner(f2,mt[i]),mt[i]*]);


end if;
ALaction:=Append(ALaction,[*f,j,ALactionf*]);
end for;


L:=Append(L,f);


F:=Append(F,K);

uu:=#Basis(K);

countergenus:=countergenus+uu;

Level:=Append(Level,j);
else
end if;
end for;

end for;
genuscorrect:=genereX0NQuotientWN(N,WN,t);

if genuscorrect eq countergenus then

CorrectJacobian:=11111111111111;

else

CorrectJacobian:=0000000000000;

end if;
M:=[*CorrectJacobian,L,F,Level, ALaction*];

return M;

end function;
````
The command ``JacobianDecompositionQuotientX0NWN(N, TN, prec, t)[2]`` gives the modular forms with q-expansion that are factor of the Jacobian, (could appear repeated if some factor of the Jacobian can appeared repeated), here N is square-free, ``TN:=[d | w_d belongs to W]`` and ``t:=#TN`` as explained before.
The command ``JacobianDecompositionQuotientX0NWN(N, TN, prec, t)[3]`` gives the number fields of the list of modular forms given in ``JacobianDecompositionQuotientX0NWN(N, TN, prec, t)[2]``, the order is the same.
````magma


FpnpointsforQuotientcurveX0NWN:=function(N,prime,JacDecomp,FieldDefinition,bound);

L:=JacDecomp;
p:=prime;
F:=FieldDefinition;
felm:=# F;
bod:=bound;


C:=ComplexField(100); R<x>:=PolynomialRing(C); pj:=0*x+1; Roo:=[**];
for j in [1 .. felm] do

if Degree(F[j]) eq 1 then

cc:=Roots(x^2-Coefficient(L[j],p)*x+p,C);

Roo:=Append(Roo,cc);

pj:=pj*(x^2-Coefficient(L[j],p)*x+p);

else

dd:=Degree(F[j]);

u:=Roots(DefiningPolynomial(F[j]),C); uu:= # u;

for m in [1 .. uu] do

f := hom< F[j] -> C | u[m][1]>;

cc2:=Roots(x^2-f(Coefficient(L[j],p))*x+p,C);

Roo:=Append(Roo,cc2);

pj:=pj*(x^2-f(Coefficient(L[j],p))*x+p);

end for;

end if;

end for;
pjdegree:=Degree(pj);

PR:=[* *];

d2:=Degree(pj);

long:= # Roo;





for nn in [1 .. bod] do s:=0;

for i in [1 .. long] do

for j in [1..2] do

if Roo[i][j][2] gt 0 then

s:=s+(Roo[i][j][2])*(Roo[i][j][1])^(nn) ;

else

s:=s;

end if;

end for;
end for;
a:=Round(1+p^(nn)-s);
PR:=Append(PR,a);
end for;

return PR;
end function;
````
The command ``FpnpointsQuotientCurve:=FpnpointsforQuotientcurveX0NWN(N,q,JacobianDecompositionQuotientX0NWN(N, TN, prec, t)[2],JacobianDecompositionQuotientX0NWN(N, TN, prec, t)[3],bound)`` gives the number of $\mathbb{F}_{q^n}$-points for the modular curve $X_0(N)/W$ for n=1 until bound, and $q$ is a prime with $p\nmid N$.
````magma



MapdegreedtoP1:=function(prime,degree,bound,FpNpointsModularCurveList)

p:=prime;
bod:=bound; deg:=degree;

PR2:=[* *];

C:=ComplexField(100); R<x>:=PolynomialRing(C);
//cearrels:=Roots(x^2-a3*x+p,C);



for i in [1..bod] do



b:=deg*(p^i+1);

PR2:=Append(PR2,b); end for;

el:=#FpNpointsModularCurveList; tt:=Min(el,bod);

NoDegreeMaptosuchP1:=[**];

for k in [1..tt] do

difference:=(FpNpointsModularCurveList[k])-(PR2[k]);

Rr:=RealField(10); difference:=Rr!difference;

case Sign(difference):
when 1:
NoDegreeMaptosuchP1:=Append(NoDegreeMaptosuchP1,[*difference,p^k*]);

end case;




end for;

return NoDegreeMaptosuchP1;

end function;
````
The command ``MapdegreedtoP1(q,degree,bound,FpnpointsQuotientCurve)`` list if any of the set $(q^k, z)$ with $k<\mathrm{bound}$ such that
$|X_0(N)/W \mathbb{F}_{q^k}|-degree\cdot |\mathbb{P}^1 \mathbb{F}_{q^k}|=z$,
where z is a positive integer. Thus no $degree$ map between the quotient modular curve $X_0(N)/W$ to the projective space $\mathbb{P}^1$ defined over $\mathbb{F}_{q^k}$, if the list is non empty (recall q is a prime not dividing N).
````magma
Squarefreepart:=function(Integer)

n:=Integer;
Pn:=#PrimeFactors(n);

for p in PrimeFactors(n) do
if p^2 in Divisors(n) then
n:=Truncate(n/(p^2));
end if;
end for;

return n;

end function;
````
The command ``Squarefreepart(d)`` gives the squarefree part of a natural number ``d``. 
We are now ready to check the inequalitites (5.46),(5.47) and (5.48). Recall that by the assumptions on W, we can consider W as a subgroup of B(M). We first check the inequalities for order(W)=2.
````magma

A1:=[ ];
q:=2;//recall that we are assuming q=2 and n=2
n:=2;
````
Recall that that we are cosidering the case q=2 and n=2
````magma
for M in [2..870] do
````
Recall that we only need to check the inequalities for M<865 (with q*M squarefree and M can have at most 3 distinct prime factors, however we check this upto the case when M has at most 4 distinct prime factors)
````magma
if IsSquarefree(q*M) eq true then

if #PrimeFactors(M) le 4 then

if ((q-1)*(SumOfDivisors(M)/12) +2^(#PrimeFactors(M))) le (2*n*(q^2+1)) then
````
This checkes values of M which satisfies the inequality (5.47) with $2^s=order(W)=2$ (recall that q=2 and n=2).
````magma
for d in Divisors(M) do
if d gt 1 then 
TN1:=[*d,1*];//here we are checking the cases with order(W)=2
t1:=#TN1;
A11:=genereX0NQuotientWN(q*M,TN1,t1);//this computes the genus of X_0(qM)
B11:=genereX0NQuotientWN(M,TN1,t1);
if A11 ge 2 then//recall that we are assuming genus(X_0(N)/W)>1
if (A11-1) ge (n*(A11-1-(2*B11))) then
````
This checkes the values of M and W with order(W)=2 that satisfy the inequality (5.48)
````magma
prec := 20; // Number of coefficients of the q-expansion
HH1 := JacobianDecompositionQuotientX0NWN(M, TN1, prec, t1);
//p:=2;// A prime number not dividing the level N
bound:=20;
FpnpointsQuotientCurve:=FpnpointsforQuotientcurveX0NWN(M,q,HH1[2],HH1[3],bound);
//print FpnpointsQuotientCurve;
degree:=n;
NondegreedmaptoP1:=MapdegreedtoP1(q,degree,bound,FpnpointsQuotientCurve);
// The command NondegreedmaptoP1 list if any of the set (q^k, z) k<bound such that |X_0(M)/W(F_{q^k})|-degree*|P^1(F_{q^k})|=z, where z is a positive integer. Thus no degree map between the quotient modular curve X_0(M)/W to the projective space P^1 defined over F_{q^k}, if the list is non empty, recall q is a prime not dividing M.

if #NondegreedmaptoP1 eq 0 then// This checks the values of M,W with order(W)=2 which satisfy the inequality (5.46) with \alpha<bound=20
````
This checks the values of M,W with order(W)=2 which satisfy the inequality (5.46) with $\alpha<bound=20$
````magma
if [q,M,d] notin A1 then
A1:=Append(A1,[q,M,d]);
end if;
end if;
end if;
end if;
end if;
end for;
end if;
````
A1 collects the values of M,W with order(W)=2 which satisfy the inequalities (5.46), (5.47) and (5.48). We now repeat the same process for order(W)=4.
````magma

if #PrimeFactors(M) in [2..4] then// since order(W)=4, M has at least 2 distinct prime divisors
if ((q-1)*(SumOfDivisors(M)/12) +2^(#PrimeFactors(M))) le (4*n*(q^2+1)) then
````
This checkes values of M which satisfies the inequality (5.47) with $2^s=order(W)=4$ (recall that q=2 and n=2).
````magma
for d1 in Divisors(M) do
if d1 gt 1 then
for d2 in Divisors(M) do
if d2 gt d1 then
if d1 ne d2 then
TN2:=[*d1,d2,Squarefreepart(d1*d2),1*];
````
Suppose W is genered by two elements $w_{d1},w_{d2}$. Since $w_{d}$ and $w_{d^\prime}$ commutes when $(d,d^\prime)=1$ and $w_{d}\circ w_{d}=1$, we have $W=<1,w_{d1},w_{d2},w_{Squarefreepart(d1\cdot d2)}>$. Hence ``TN:=[1,d1,d2,Squarefreepart(d1*d2)]``.  
````magma
t2:=#TN2;
A12:=genereX0NQuotientWN(q*M,TN2,t2);
B12:=genereX0NQuotientWN(M,TN2,t2);
if A12 ge 2 then
if (A12-1) ge (n*(A12-1-(2*B12))) then
````
This checkes the values of M and W with order(W)=4 that satisfy the inequality (5.48)
````magma
prec := 20; // Number of coefficients of the q-expansion
HH2 := JacobianDecompositionQuotientX0NWN(M, TN2, prec, t2);
//p:=2;// A prime number not dividing the level N
bound:=20;
FpnpointsQuotientCurve:=FpnpointsforQuotientcurveX0NWN(M,q,HH2[2],HH2[3],bound);
//print FpnpointsQuotientCurve;
degree:=n;
NondegreedmaptoP1:=MapdegreedtoP1(q,degree,bound,FpnpointsQuotientCurve);
if #NondegreedmaptoP1 eq 0 then
````
This checks the values of M,W with order(W)=4 which satisfy the inequality (5.46) with $\alpha<bound=20$
````magma
E:=[d1,d2,Squarefreepart(d1*d2)];
````
The value E, we just got satisfies the inequalities (5.46), (5.47) and (5.48) with order(W)=4 and $W=<1,w_{d1},w_{d2},w_{Squarefreepart(d1\cdot d2)}>$. We now find some suitable generators of W. Note that if e1:=min({d1,d2,Squarefreepart($d1\cdot d2$)}) and e2:=min({d1,d2,Squarefreepart($d1\cdot d2$)}\\ {e1}), then $w_{e1}$ and $w_{e2}$ generate $W$.
````magma
e1:=Minimum(E);
E1:=Exclude(E,e1);
e2:=Minimum(E1);
if [q,M,e1,e2] notin A1 then
A1:=Append(A1,[q,M,e1,e2]);
end if;
end if;
end if;
end if;
end if;
end if;
end for;
end if;
end for;
end if;
end if;
````
A1 collects the values of M,W with $order(W)\in \{2,4\}$ which satisfy the inequalities (5.46), (5.47) and (5.48). We now repeat the same process for order(W)=8.
````magma
if #PrimeFactors(M) in [3..4] then// since order(W)=8, M has at least 3 distinct prime divisors
if ((q-1)*(SumOfDivisors(M)/12) +2^(#PrimeFactors(M))) le (8*n*(q^2+1)) then
````
This checkes values of M which satisfies the inequality (5.47) with $2^s=order(W)=8$ (recall that q=2 and n=2).
````magma
for d1 in Divisors(M) do
if d1 gt 1 then
for d2 in Divisors(M) do
if d2 gt d1 then
if d1 ne d2 then
for d3 in Divisors(M) do
if d3 gt d2 then
if d3 notin [d1,d2,Squarefreepart(d1*d2)] then
TN3:=[d1,d2,d3,Squarefreepart(d1*d2), Squarefreepart(d1*d3), Squarefreepart(d2*d3),Squarefreepart(d1*d2*d3),1];
````
Suppose W is genered by three elements $w_{d1},w_{d2},w_{d3}$. Since $w_{d}$ and $w_{d^\prime}$ commutes when $(d,d^\prime)=1$ and $w_{d}\circ w_{d}=1$, we have $W=<1,w_{d1},w_{d2},w_{d3},w_{Squarefreepart(d1\cdot d2)}, w_{Squarefreepart(d1\cdot d3)}, w_{Squarefreepart(d2\cdot d3)}, w_{Squarefreepart(d1\cdot d2 \cdot d3)}>$. Hence ``TN:=[1,d1,d2,d3,Squarefreepart(d1*d2), Squarefreepart(d1*d3), Squarefreepart(d2*d3),Squarefreepart(d1*d2*d3)]``.  
````magma
t3:=#TN3;
A13:=genereX0NQuotientWN(q*M,TN3,t3);
B13:=genereX0NQuotientWN(M,TN3,t3);
if A13 ge 2 then
if (A13-1) ge (n*(A13-1-(2*B13))) then
````
This checkes the values of M and W with order(W)=8 that satisfy the inequality (5.48)
````magma
prec := 20; // Number of coefficients of the q-expansion
HH3 := JacobianDecompositionQuotientX0NWN(M, TN3, prec, t3);
//p:=2;// A prime number not dividing the level N
bound:=20;
FpnpointsQuotientCurve:=FpnpointsforQuotientcurveX0NWN(M,q,HH3[2],HH3[3],bound);
//print FpnpointsQuotientCurve;
degree:=n;
NondegreedmaptoP1:=MapdegreedtoP1(q,degree,bound,FpnpointsQuotientCurve);
if #NondegreedmaptoP1 eq 0 then
````
This checks the values of M,W with order(W)=8 which satisfy the inequality (5.46) with $\alpha<bound=20$
````magma
F:=[d1,d2,d3,Squarefreepart(d1*d2), Squarefreepart(d1*d3), Squarefreepart(d2*d3),Squarefreepart(d1*d2*d3)];
````
The value E, we just got satisfies the inequalities (5.46), (5.47) and (5.48) with order(W)=8 and $W=<1,w_{d1},w_{d2},w_{d3},w_{Squarefreepart(d1\cdot d2)}, w_{Squarefreepart(d1\cdot d3)}, w_{Squarefreepart(d2\cdot d3)}, w_{Squarefreepart(d1\cdot d2 \cdot d3)}>$. We now find some suitable generators of W. Note that if e1:=min({d1,d2,Squarefreepart($d1\cdot d2$), Squarefreepart($d1\cdot d3$), Squarefreepart($d3\cdot d3$), Squarefreepart($d1\cdot d2\cdot d3$)}), e2:=min({d1,d2,Squarefreepart($d1\cdot d2$), Squarefreepart($d1\cdot d3$), Squarefreepart($d3\cdot d3$), Squarefreepart($d1\cdot d2\cdot d3$)}\\ {e1}) and e3:=e2:=min({d1,d2,Squarefreepart($d1\cdot d2$), Squarefreepart($d1\cdot d3$), Squarefreepart($d3\cdot d3$), Squarefreepart($d1\cdot d2\cdot d3$)}\\ {e1,e2}) , then $w_{e1}$, $w_{e2}$ and $w_{e3}$ generate $W$.
````magma
f1:=Minimum(F);
F1:=Exclude(F,f1);
f2:=Minimum(F1);
F2:=Exclude(F1,f2);
F3:=Exclude(F2,Squarefreepart(f1*f2));
f3:=Minimum(F3);
if [q,M,f1,f2,f3] notin A1 then
A1:=Append(A1,[q,M,f1,f2,f3]);
end if;
end if;
end if;
end if;
end if;
end if;
end for;
end if;
end if;
end for;
end if;
end for;
end if;
end if;
````
A1 collects the values of M,W with $order(W)\in \{2,4,8\}$ which satisfy the inequalities (5.46), (5.47) and (5.48). We now repeat the same process for order(W)=16.
````magma

if #PrimeFactors(M) eq 4 then//since order(W)=16, the only possibility is M has exactly 4 distict prime divisors
if ((q-1)*(SumOfDivisors(M)/12) +2^(#PrimeFactors(M))) le (16*n*(q^2+1)) then
````
This checkes values of M which satisfies the inequality (5.47) with $2^s=order(W)=8$ (recall that q=2 and n=2).
````magma
TN4:=Divisors(M);
````
Since order(W)=Number od distinct prime divisors of M, we can take TN:=[d|d divides M] i.e., ``TN:=Divisors(M)``
````magma
t4:=#TN4;
A14:=genereX0NQuotientWN(q*M,TN4,t4);
B14:=genereX0NQuotientWN(M,TN4,t4);
if A14 ge 2 then
if (A14-1) ge (n*(A14-1-(2*B14))) then
````
This checkes the values of M and W with order(W)=16 that satisfy the inequality (5.48)
````magma
prec := 20; // Number of coefficients of the q-expansion
HH4 := JacobianDecompositionQuotientX0NWN(M, TN4, prec, t4);
//p:=2;// A prime number not dividing the level N
bound:=20;
FpnpointsQuotientCurve:=FpnpointsforQuotientcurveX0NWN(M,q,HH4[2],HH4[3],bound);
//print FpnpointsQuotientCurve;
degree:=n;
NondegreedmaptoP1:=MapdegreedtoP1(q,degree,bound,FpnpointsQuotientCurve);
if #NondegreedmaptoP1 eq 0 then
````
This checks the values of M,W with order(W)=16 which satisfy the inequality (5.46) with $\alpha<bound=20$
````magma
A1:=Append(A1,[q,M,PrimeFactors(M)[1], PrimeFactors(M)[2], PrimeFactors(M)[3], PrimeFactors(M)[4]]);
end if;
end if;
end if;
end if;
end if;

end if;

//end if;
end if;
end for;
A1;
````
Finally, A1 collects the values of M,W with M<870 and $order(W)\in \{2,4,8,16\}$ which satisfy the inequalities (5.46), (5.47) and (5.48). The final output consist of elements of the form ``[q,M,n1,n2,...,nk]``, where the 1st and 2nd co-ordinate  i.e., q  and M correspond to q (here q=2) and M respectively as in inequalities (5.46), (5.47) and (5.48) and the remaining co-ordinates i.e., n1,n2,...,nk correspond to the subgroup W generated by the Atkin-Lehner invoutions w_n1,w_n2,...,w_nk.

````magma


``````````````````
